#!/usr/bin/env fish

function header_banner -a title
    set title_length (math (string length $title) + 4)
    set stars (string repeat -n $title_length '*')
    set_color brblue
    echo
    echo $stars
    echo "* $title *"
    echo $stars
    set_color normal
end

function install_packages
    header_banner "Installing common packages"
    if command -v apt >/dev/null 2>&1
        # Debian, Ubuntu and derivatives
        set -l packages bat direnv eza fd-find fzf git less most ripgrep
        sudo apt install $packages
    else if command -v pacman >/dev/null 2>&1
        # Arch Linux and derivatives
        set -l packages "bat direnv eza fd fzf git less most ripgrep uv"
        sudo pacman -Sy $packages
    else if command -v dnf >/dev/null 2>&1
        # Fedora and other RPM-based distros
        echo "FIXME: RPM-based distro detected, but no packages defined to be installed."
        return 2
    else
        echo "No supported package manager found, unable to automatically install common packages."
        return 1
    end
end

function setup_fish_vendor
    # fish plugins I use are included verbatim in .config/fish/vendor
    # they must be copied to .config/fish to become usable
    header_banner "Setting up fish vendored plugins"

    # disable fzf plugin on slow systems
    if test "$disable_fzf" = true
        echo "Disabling fzf-fish plugin"
        rm -rf $HOME/.config/fish/conf.d/fzf.fish $HOME/.config/fish/vendor/fzf-fish
    end

    set vendor_src $HOME/.config/fish/vendor
    set vendor_tgt $HOME/.config/fish/
    cp -prf $vendor_src/* $vendor_tgt
    or exit 2
    echo "Copied fish vendored plugins from $vendor_src to $vendor_tgt"
end

function set_fish_universal_vars
    header_banner "Setting fish universal variables"

    echo "Unsetting fish_greeting"
    set -U fish_greeting

    set enabled_features ampersand-nobg-in-token,qmark-noglob,regex-easyesc,stderr-nocaret
    echo "Enabling fish_features:" $enabled_features
    set -U fish_features $enabled_features

    echo "Enabling pure prompt show root prefix"
    set -U pure_show_prefix_root_prompt true

    echo "Enabling pure prompt show error code in prompt"
    set -U pure_separate_prompt_on_error true
end

function set_default_shell
    header_banner "Set fish as default shell"
    set current_default_shell (getent passwd $LOGNAME | cut -d: -f7)
    if test $current_default_shell != /usr/bin/fish
        echo "Setting /usr/bin/fish as default shell, enter password to confirm"
        chsh -s /usr/bin/fish
    else
        echo "fish is already the default shell, nothing to do."
    end
end

function setup_emacs
    header_banner "Set up Emacs configuration"
    set -l emacs_home $HOME/.config/.emacs
    if type -q emacs
        echo Emacs installation found at (which emacs), version (emacs --version)[1]
        if test -e $emacs_home
            echo "Emacs config already found at $emacs_home, skipping further configuration"
        else
            set -l repo https://github.com/haplo/dotemacs
            echo "Downloading Emacs config from $repo to $emacs_home"
            git clone $repo $emacs_home
        end
    else
        echo "No Emacs installation detected, skipping Emacs configuration"
    end
end

function setup_rustup
    header_banner "Set up rustup"
    if type -q rustup
        echo "Detected rustup at $(which rustup), executing personal setup..."
        echo "--- rustup default stable"
        rustup default stable

        echo "--- rustup component add rust-src"
        rustup component add rust-src

        echo
        echo "--- rustup component add rust-analyzer"
        rustup component add rust-analyzer

        set local_rust_analyzer $HOME/.local/bin/rust-analyzer
        if ! test -e "$local_rust_analyzer"
            set rustup_rust_analyzer (rustup which --toolchain stable rust-analyzer)
            echo
            echo "--- symlinking $rustup_rust_analyzer to $local_rust_analyzer"
            mkdir -p (dirname $local_rust_analyzer)
            ln -s $rustup_rust_analyzer $local_rust_analyzer
        end
    else
        echo "rustup not found."
    end
end

install_packages
setup_fish_vendor
set_fish_universal_vars
set_default_shell
setup_emacs
setup_rustup
